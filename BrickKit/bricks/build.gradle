plugins {
    id 'com.android.library'
    id 'checkstyle'
    id 'maven-publish'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'

ext {
    PUBLISH_GROUP_ID = "com.wayfair"
    PUBLISH_VERSION = getCurrentVersion()
    PUBLISH_ARTIFACT_ID = 'brickkit-android'
}

apply from: "${project.rootDir}/scripts/publish-mavencentral.gradle"

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 23
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures.dataBinding = true

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = 11
    }

    namespace 'com.wayfair.brickkit'

    lint {
        abortOnError true
        warningsAsErrors true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    testImplementation 'junit:junit:4.13.2'

    kaptAndroidTest 'androidx.databinding:databinding-compiler:7.3.1'

    androidTestImplementation 'org.mockito:mockito-core:4.8.1'
    androidTestImplementation("org.mockito.kotlin:mockito-kotlin:4.0.0") {
        exclude group: "org.mockito", module: "mockito-core"
    }
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline:2.28.3'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0') {
        exclude group: 'androidx.annotation', module: 'annotation'
    }
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}

check.dependsOn("checkstyle")

gradle.projectsEvaluated {
    createDebugAndroidTestCoverageReport.dependsOn("connectedDebugAndroidTest")
}

allOpen {
    annotation 'com.wayfair.brickkit.OpenForTesting'
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/quality/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath
    source 'src/main'
    include '**/*.java'
    classpath = files() as FileCollection
}

static def getCurrentVersion() {
    return "git describe --abbrev=0 --tag".execute().text.trim()
}

// This is mandatory
group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION
